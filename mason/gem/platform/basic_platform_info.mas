<%doc>

=head1 NAME 
 
 basic_platform_info.mas
 Mason component to show the basic information for platform web page.

=cut

=head1 VERSION 

0.1

=cut 

=head1 DESCRIPTION

 Mason component to show the basic information for platform web page.

 - Mason root page = platform_detail.mas
 - Perl controller = platform.pl

 This component use out schema dbi connection to get data about CXGN::People::Person

=cut
 
=head 1 AUTHOR
 
 Aureliano Bombarely (ab782@cornell.edu)

=cut 

</%doc>


<%args>
$dbh
$platform            
</%args>

<%init>
use strict;
use warnings;

use CXGN::GEM::Schema;
use CXGN::Biosource::Sample;
use CXGN::Page::FormattingHelpers  qw/ info_section_html info_table_html columnar_table_html page_title_html html_break_string /;
</%init>

<%perl>

my $basic_info_content;
my $platform_accession;

my $default_message = '<i><font color="gray">data not available</font></i>';

## If there aren't any experiment_row that comes from experimental_design_detail.mas, it will return a message.

if (defined $platform->get_platform_id()) {

   ## Get the schema object
   my $schema = $platform->get_schema();   
   
   my $platform_name = $platform->get_platform_name();
   my $description = $platform->get_description();
   $platform_accession = $platform_name;

   my $template_count = $platform->count_templates();
   my $probe_count = $platform->count_probes();

   my $techtype = $platform->get_technology_type();
   my $techtype_name = $techtype->get_technology_name();

   ## Get the platform design links

   my @design_links;
   my @sample_id_list = $platform->get_design_list();
   foreach my $sample_id (@sample_id_list) {
       my $sample = CXGN::Biosource::Sample->new($schema, $sample_id);
       my $sample_name = $sample->get_sample_name();
       my $sample_link = '/biosource/sample.pl?id=' . $sample_id;
       my $sample_html = "<a href=$sample_link>$sample_name</a>";

       push @design_links, $sample_html;
   }
   my $design_links = join('<br>', @design_links) || $default_message;

   ## Get the external links

   my @dbxref_list_id = $platform->get_dbxref_list();
   my @dbxref_html_list = ();

   foreach my $dbxref_id (@dbxref_list_id) {
       my ($dbxref_row) = $schema->resultset('General::Dbxref')
	                         ->search({ dbxref_id => $dbxref_id });

       if (defined $dbxref_row) {
	   my ($db_row) = $schema->resultset('General::Db')
                                 ->search({ db_id => $dbxref_row->get_column('db_id') });
            
	   my $dbxref_link = $db_row->get_column('urlprefix') . 
		             $db_row->get_column('url') . 
			     $dbxref_row->get_column('accession');

	   my $dbxref_html = $db_row->get_column('name') . 
                             ": <a href=$dbxref_link>" . 
			     $dbxref_row->get_column('accession')."</a>";

	   push @dbxref_html_list, $dbxref_html;
       }
   }
    
   my $dbxref_html_list = join('<br>', @dbxref_html_list) || $default_message;

   ## Get information of the contact

   my $person_id = $platform->get_contact_id();
   my ($complete_name, $contact_email);
   if (defined $person_id) {

       my $person = CXGN::People::Person->new($dbh, $person_id);
      
       my $salutation = $person->get_salutation() || 'Dr.';
       my $first_name = $person->get_first_name();
       my $last_name = $person->get_last_name();
       $contact_email = $person->get_contact_email || $default_message;
       if (defined $salutation && defined $first_name && defined $last_name) {
           $complete_name = "$salutation $first_name $last_name";
       } else {
           $complete_name = $default_message;
       }

   } else {
      $complete_name = $default_message;
      $contact_email = $default_message;
   }



   ## Create the HTML table

   $basic_info_content = <<HTML;

   <table width="100%">
   	   <tr> <td width="25%"> <b>Platform name:</b>            </td> <td> $platform_name </td></tr>
	   <tr> <td width="25%"> <b>Technology type:</b>          </td> <td> $techtype_name </td></tr>
           <tr> <td width="25%"> <b>Description:</b>              </td> <td> $description </td></tr>
	   <tr> <td width="25%"> <b>Template number:</b>          </td> <td> $template_count </td></tr>
	   <tr> <td width="25%"> <b>Probe number:</b>             </td> <td> $probe_count </td></tr>
	   <tr> <td width="25%"> <b>Design datasets:</b>          </td> <td> $design_links </td></tr>
	   <tr> <td width="25%"> <b>Platform external links:</b>  </td> <td> $dbxref_html_list </td></tr>
	   <tr> <td width="25%"> <b>Contact info:</b>             </td> <td> <b>Name:</b> $complete_name </td></tr>
           <tr> <td width="">                                     </td> <td> <b>E-mail:</b> $contact_email </td></tr>

   </table>

HTML

} else {
   $basic_info_content = "<big>There aren't any platform data for the specified parameters.</big>";
}   

my $basic_info_html;
if (defined $platform_accession) {
   $basic_info_html = "<center><big><b>Expression Platform: $platform_accession</b></big></center><br>";
}

$basic_info_html .= info_section_html( title => "Platform Basic Information", contents => $basic_info_content );

</%perl>

<% $basic_info_html %>

<& 
   /util/import_javascript.mas, 
   classes => 'CXGN.Effects' 
&>
